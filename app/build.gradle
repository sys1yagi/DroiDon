apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'com.cookpad.android.licensetools'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.github.gfx.ribbonizer'

apply from: "${project.rootDir}/app/mastodon.gradle"

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    dataBinding {
        enabled true
    }

    defaultConfig {
        applicationId "com.sys1yagi.mastodon.android"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled = true
        buildConfigField "String", "client_id", "\"$CLIENT_ID\""
        buildConfigField "String", "client_secret", "\"$CLIENT_SECRET\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

configurations {
    all {
        resolutionStrategy {
            force "com.android.support:support-annotations:${supportLibraryVersion}"
            force "com.google.code.findbugs:jsr305:3.0.2"
        }
    }
}

dependencies {

    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    // UI
    compile 'com.github.sys1yagi.fragment-creator:library:0.8.2'
    kapt 'com.github.sys1yagi.fragment-creator:processor:0.8.2'
    kapt "com.android.databinding:compiler:$gradleToolsVersion"
    compile "com.android.support:appcompat-v7:$supportLibraryVersion"
    compile "com.android.support:design:$supportLibraryVersion"
    compile "com.android.support:cardview-v7:$supportLibraryVersion"
    compile "com.android.support:support-vector-drawable:$supportLibraryVersion"
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.google.android:flexbox:0.2.5'
    compile('com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:0.10.5@aar') {
        transitive = true
    }
    compile 'me.mvdw.recyclerviewmergeadapter:recyclerviewmergeadapter:2.0.0'

    // Rx
    compile "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
    compile "io.reactivex.rxjava2:rxjava:$rxJavaVersion"

    // Dagger
    compile "com.google.dagger:dagger:$dagger2Version"
    kapt "com.google.dagger:dagger-compiler:$dagger2Version"
    compile "com.google.dagger:dagger-android-support:$dagger2Version"
    kapt "com.google.dagger:dagger-android-processor:$dagger2Version"

    // Network
    compile "com.squareup.okhttp3:okhttp:$okhttpVersion"
    compile project(':mastodon4j-rx')

    compile 'com.facebook.fresco:fresco:1.1.0'

    // DB
    compile("com.github.gfx.android.orma:orma:$ormaVersion") {
        exclude group: 'io.reactivex.rxjava2', module: 'rxjava'
        exclude group: 'io.reactivex.rxjava2', module: 'rxandroid'
    }
    kapt "com.github.gfx.android.orma:orma-processor:$ormaVersion"

    // Util
    compile "com.google.code.gson:gson:$gsonVersion"
    compile 'com.jakewharton.threetenabp:threetenabp:1.0.5'

    compile 'com.jakewharton.timber:timber:4.5.1'
    compile 'com.android.support:multidex:1.0.1'

    // Test
    testCompile 'junit:junit:4.12'
    testCompile "org.robolectric:robolectric:3.3.2"
    testCompile "org.assertj:assertj-core:3.6.2"
    testCompile("com.squareup.assertj:assertj-android:$assertjAndroidVersion") {
        exclude group: 'com.android.support'
    }
    testCompile("com.squareup.assertj:assertj-android-appcompat-v7:$assertjAndroidVersion") {
        exclude group: 'com.android.support'
    }
    testCompile("com.squareup.assertj:assertj-android-recyclerview-v7:$assertjAndroidVersion") {
        exclude group: 'com.android.support'
    }
    testCompile('com.github.sys1yagi:kmockito:0.1.2') {
        exclude group: 'org.mockito', module: 'mockito-core'
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib'
    }
    testCompile("com.squareup.okhttp3:mockwebserver:$okhttpVersion") {
        exclude group: 'com.squareup.okio', 'module': 'okio'
        exclude group: 'com.squareup.okhttp', 'module': 'okhttp'
    }
    testCompile 'org.mockito:mockito-core:2.7.22'

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}
repositories {
    mavenCentral()
}
